// Copyright 2024 Cornell University
// released under MIT License
// author: Nikil Shyamunder <nikil.shyamsunder@gmail.com>
// author: Kevin Laeufer <laeufer@cornell.edu>

WHITESPACE = _{ " " | NEWLINE }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

// Literals
integer = ${ bin_integer | oct_integer | hex_integer | dec_integer }
  bin_integer = ${ "0" ~ ("b" | "B") ~ ("_"? ~ ASCII_BIN_DIGIT)+ }
  oct_integer = ${ "0" ~ ("o" | "O") ~ ("_"? ~ ASCII_OCT_DIGIT)+ }
  hex_integer = ${ "0" ~ ("x" | "X") ~ ("_"? ~ ASCII_HEX_DIGIT)+ }
  dec_integer = ${ ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)* }
bool = @{ "true" | "false"}

// Expressions
primary = _{ integer | bool | "(" ~ expr ~ ")" | path_id }
atom = _{primary | unary_op? ~ primary}
unary_op = _ { negate | not }
    negate = { "-" }
    not = { "!" }
bin_op = _{ neq | leq | eq | log_and | log_or | shift_left | shift_right | add | subtract | multiply | divide | mod }
    neq = { "!=" }
    leq = { "<= "}
    eq = { "== "}
    add = { "+" }
    log_and = { "&&" }
    log_or = { "||" }
    subtract = { "-" }
    multiply = { "*" }
    divide = { "/" }
    mod = { "%" }
    shift_left = { "<<" }
    shift_right = { ">>" }

expr = { atom ~ (bin_op ~ atom)* }

id = @{ ASCII_ALPHA  ~ (ASCII_ALPHANUMERIC | "_" )* }
path_id = @{ id ~ ("." ~ id)* }
tpe = { "u32" | "u1" }
assign = {path_id ~ ":=" ~ (expr | path_id) ~ ";" }
cmd = { path_id ~ "(" ~ ")" ~ ";"}
stmt = _{ (assign | cmd | while | cond) }
dir = _{"in" | "out" }
arg = { dir ~ id ~ ":" ~ tpe }
arglist = { (arg ~ "," ~ arglist) | arg ~ ","? }
type_param = { "<" ~ id ~ ":" ~ id ~ ">" }

// Loops
while = { "while" ~ expr ~ "{" ~ stmt* ~ "}" }

// Conditionals
if = { "if" ~ expr ~ "{" ~ stmt* ~ "}" }
else_if = { "else" ~ "if" ~ expr ~ "{" ~ stmt* ~ "}"}
else = { "else" ~ "{" ~ stmt* ~ "}" }
cond = { if ~ else_if* ~ else? }

// Structs
struct = { "struct" ~ id ~ "{" ~ arglist? ~ "}" }

fun = { "fn" ~ id ~ type_param? ~ "(" ~ arglist ~ ")" ~ "{" ~ stmt* ~ "}" }
file = { SOI ~ (fun | struct)* ~ EOI }