// Copyright 2025 Cornell University
// released under MIT License
// author: Ernest Ng <eyn5@cornell.edu>

WHITESPACE = _{ " " | NEWLINE }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

ident = { ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }

// Arguments to functions must be integer literals (decimal/binary/hex)
binary_integer = ${ "0" ~ ("b" | "B") ~ ("_"? ~ ASCII_BIN_DIGIT)+ }
hex_integer = ${ "0" ~ ("x" | "X") ~ ("_"? ~ ASCII_HEX_DIGIT)+ }
decimal_integer = ${ ASCII_DIGIT ~ ("_"? ~ ASCII_DIGIT)* }

arg = ${ binary_integer | hex_integer | decimal_integer }
arglist = { (arg ~ "," ~ arglist) | arg ~ ","? }

transaction = { ident ~ "(" ~ arglist? ~ ")" ~ ";" }

file = { SOI ~ transaction* ~ EOI }